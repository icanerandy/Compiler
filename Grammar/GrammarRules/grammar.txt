<程序> -> <声明语句列表> <MAIN函数定义> <函数定义列表>
<MAIN函数定义> -> MAIN_ID ( ) <复合语句>
<声明语句列表> -> <声明语句> <声明语句列表>
<声明语句列表> -> <epsilon>
<函数定义列表> -> <函数定义> <函数定义列表>
<函数定义列表> -> <epsilon>
<函数定义> -> <函数类型> <函数名> ( <函数定义形参列表> ) <复合语句>
<函数定义形参列表> -> <函数定义形参>
<函数定义形参列表> -> <epsilon>
<函数定义形参> -> <数据类型> <变量>
<函数定义形参> -> <数据类型> <变量> , <函数定义形参>
<复合语句> -> { <语句表> }
<语句表> -> <语句>
<语句表> -> <语句> <语句表>
<执行语句> -> <数据处理语句>
<执行语句> -> <控制语句>
<数据处理语句> -> <表达式> ;
<控制语句> -> <if语句>
<控制语句> -> <for语句>
<控制语句> -> <while语句>
<控制语句> -> <do-while语句>
<控制语句> -> <return语句>
<if语句> -> if ( <表达式> ) <循环语句> <if-tail>
<if-tail> -> else <循环语句>
<if-tail> -> <epsilon>
<for语句> -> for ( <表达式> ; <表达式> ; <表达式> ) <循环语句>
<while语句> -> while ( <表达式> ) <循环语句>
<do-while语句> -> do <循环用复合语句> while ( <表达式> ) ;
<循环语句> -> <声明语句>
<循环语句> -> <循环执行语句>
<循环语句> -> <循环用复合语句>
<循环用复合语句> -> { <循环语句表> }
<循环语句表> -> <循环语句>
<循环语句表> -> <循环语句> <循环语句表>
<循环执行语句> -> <if语句>
<循环执行语句> -> <for语句>
<循环执行语句> -> <while语句>
<循环执行语句> -> <do-while语句>
<循环执行语句> -> <return语句>
<循环执行语句> -> <break语句>
<循环执行语句> -> <continue语句>
<循环执行语句> -> <数据处理语句>
<return语句> -> return ;
<return语句> -> return <表达式> ;
<break语句> -> break ;
<continue语句> -> continue ;
<语句> -> <声明语句>
<语句> -> <执行语句>
<声明语句> -> <值声明>
<声明语句> -> <函数声明>
<值声明> -> <常量声明>
<值声明> -> <变量声明>
<常量声明> -> const <数据类型> <常量声明表>
<数据类型> -> int
<数据类型> -> float
<数据类型> -> char
<数据类型> -> String
<常量声明表> -> <常量> = <常数> ;
<常量声明表> -> <常量> = <常数> , <常量声明表>
<变量声明> -> <数据类型> <变量声明表>
<变量声明表> -> <单变量声明> ;
<变量声明表> -> <单变量声明> , <变量声明表>
<单变量声明> -> <变量>
<单变量声明> -> <变量> = <布尔表达式>
<函数声明> -> <函数类型> <函数名> ( <函数声明形参列表> ) ;
<函数类型> -> <数据类型>
<函数类型> -> void
<函数声明形参列表> -> <函数声明形参>
<函数声明形参列表> -> <epsilon>
<函数声明形参> -> <数据类型>
<函数声明形参> -> <数据类型> , <函数声明形参>
<表达式> -> <赋值表达式>
<表达式> -> <简单表达式>
<简单表达式> -> <布尔表达式>
<赋值表达式> -> <变量> = <布尔表达式>
<布尔表达式> -> <布尔表达式> || <布尔项>
<布尔表达式> -> <布尔项>
<布尔项> -> <布尔项> && <布尔因子>
<布尔项> -> <布尔因子>
<布尔因子> -> ! <布尔因子>
<布尔因子> -> <关系表达式>
<关系表达式> -> <算术表达式>
<关系表达式> -> <算术表达式> <关系运算符> <算术表达式>
<关系运算符> -> >
<关系运算符> -> <
<关系运算符> -> >=
<关系运算符> -> <=
<关系运算符> -> ==
<关系运算符> -> !=
<算术表达式> -> <项> + <算术表达式>
<算术表达式> -> <项> - <算术表达式>
<算术表达式> -> <项>
<项> -> <因子> * <项>
<项> -> <因子> / <项>
<项> -> <因子> % <项>
<项> -> <因子>
<因子> -> ( <算术表达式> )
<因子> -> <常数>
<因子> -> <字符常量>
<因子> -> <字符串常量>
<因子> -> <变量>
<因子> -> <函数调用>
<函数调用>  -> <函数名> ( <实参列表> )
<实参列表> -> <表达式>
<实参列表> -> <表达式> , <实参列表>
<实参列表> -> <epsilon>